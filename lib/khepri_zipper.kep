/**
 * @fileOverview Khepri AST zipper.
 */
package (
    khepriZipper
    
    getChild
    getChildren
    construct)
with
    import 'nu-stream::gen' {range},
    import 'nu-stream::stream' {foldl from NIL},
    import 'neith::tree' {treeZipper},
    import 'khepri-ast::node' {Node modify}
in {

var buildArray = \pairs ->
    foldl(
        \p {key value} -> {
            p.(key) = value;
            return p;
        },
        [],
        pairs);

/**
 * Get the children of a node
 */
getChildren = \node ->
    ?node
        :?Array.isArray(node)
            :range(0, node.length)
            :from(node.children)
        :NIL;

/**
 * Get the child of a node.
 */
getChild = \node k -> node.(k);

/**
 * Reconstruct a node
 */
construct = \node pairs values ->
    ?node
        :?Array.isArray(node)
            :buildArray(pairs)
            :modify(node, values(), {})
        :node;

/**
 * Create an new Khepri AST Zipper.
 * 
 * @param root Root of AST.
 */
khepriZipper = treeZipper@(
    getChildren,
    getChild,
    construct);

}