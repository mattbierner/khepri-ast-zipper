/**
 * @fileOverview Khepri AST zipper.
 */
package (
    khepriZipper)
with
    import 'nu-stream::gen' {range},
    import 'nu-stream::stream' {foldl from NIL},
    import 'neith::tree' {treeZipper},
    import 'khepri-ast::node' {Node modify}
in {

var buildArray = \pairs ->
    foldl(
        \p, {key value} -> {
            p[key] = value;
            return p;
        },
        [],
        pairs);

/**
 * Create an new Khepri AST Zipper.
 * 
 * @param root Root of AST.
 */
khepriZipper = (treeZipper,
    \node ->
        ?node
            :?Array.isArray(node)
                :range(0, node.length)
                :from(node.children)
            :NIL,
    
    \n k -> n[k],
    
    \node pairs values ->
        ?node instanceof Node
            :modify(node, values(), {})
            :buildArray(pairs));

}